apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

// 执行 ./gradlew :publish:publishReleasePublicationToMavenRepository
afterEvaluate { project ->
    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        classifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        classifier = 'javadoc'
    }
    publishing {
        // 定义发布什么
        publications {
            mavenJava(MavenPublication) {
                // groupId = project.group
                groupId = 'io.github.Joehaivo'
                artifactId = 'jpublish'
                version = '0.0.1'
                // groupId,artifactId,version，如果不定义，则会按照以上默认值执行
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    // 构件名称
                    // 区别于artifactId，可以理解为artifactName
                    name = 'JPublish'
                    // 构件描述
                    description = 'build android module & publish android .aar to maven repository'
                    // 构件主页
                    url = 'https://github.com/Joehaivo/JPublish'
                    // 许可证名称和地址
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    // 开发者信息
                    developers {
                        developer {
                            name = 'haivo'
                            email = 'haivo@foxmail.com'
                        }
                    }
                    // 版本控制仓库地址
                    scm {
                        connection = "scm:svn:http://github.com/Joehaivo/JPublish"   //项目联系地址
                        developerConnection = "scm:svn:https://github.com/Joehaivo/JPublish"  //项目仓库地址
                        url = "http://github.com/Joehaivo/JPublish" //项目主页地址
                    }
                }
            }
        }
        // 定义发布到哪里
        repositories {
            maven {
                // 名字可随意，会显示在 task 中
                name = 'sonatypeRepository'
                //url = "https://s01.oss.sonatype.org/content/repositories/snapshots/" //快照上传地址
                // url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/" 正式服务器
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    //sonatype平台账号密码 密码有特殊字符可用 \ 转义
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
        }
    }
    signing {
        sign publishing.publications.mavenJava
    }
}


javadoc {
    // <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
    // 防止本地打开中文乱码
    options.addStringOption("charset", "UTF-8")
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compileOnly 'com.android.tools.build:gradle:4.0.2'
    compileOnly 'com.android.tools.build:gradle-api:4.0.2'
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"